plugins {
    id "application"
    id "org.jetbrains.kotlin.jvm" version "1.9.22"
    id "org.openjfx.javafxplugin" version "0.1.0"
}

version = "1.0-SNAPSHOT"
application.getMainClass().set("game.Launcher")
application.applicationDefaultJvmArgs = ["-Dprism.maxvram=4000M"]

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/samaelheaven/zenith")
        def user = project.findProperty("gpr.user") ?: System.getenv("USERNAME_GITHUB")
        def token = project.findProperty("gpr.token") ?: System.getenv("TOKEN_GITHUB")
        if (user != null && token != null) {
            credentials {
                username = user
                password = token
            }
        }
    }
}

import org.gradle.internal.os.OperatingSystem

def javafxVersion = "21.0.3"
def lwjglNatives = ""

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        def osArch = System.getProperty("os.arch")
        lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64") ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}" : "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        def osArch = System.getProperty("os.arch")
        lwjglNatives = osArch.contains("64") ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}" : "natives-windows-x86"
        break
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

kotlin {
    jvmToolchain(17)
}

javafx {
    version = javafxVersion
    modules = ["javafx.controls", "javafx.fxml", "javafx.swing"]
}

dependencies {
    implementation "org.samaelheaven:zenith:1.0-SNAPSHOT"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes "Main-Class": application.getMainClass()
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}